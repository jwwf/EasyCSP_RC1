<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25" zv="Cache for UNIX (Red Hat Enterprise Linux for x86-64) 2015.2.2 (Build 805U)" ts="2016-08-27 08:28:15">
<Class name="EasyCSP.Views.Default.RecordView">
<Description><![CDATA[
Inspect an entire record. This is the default system view. To override this
view, create a class in your local Views.<i>controllerName.</i> package folder.
Your class must have the same signature and an entry-point method named run().


  <p>Copyright (c) 2011 James W "Bill" Westley-Farrell</p>

  <p>Licensed under the Apache License, Version 2.0 (the "License");
  you may not use this file except in compliance with the License.
  You may obtain a copy of the License at</p>

      <p><a href=http://www.apache.org/licenses/LICENSE-2.0">Apache License 2.0</a>

  <p>Unless required by applicable law or agreed to in writing, software
  distributed under the License is distributed on an "AS IS" BASIS,
  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
  See the License for the specific language governing permissions and
  limitations under the License.</p>]]></Description>
<Super>EasyCSP.Views.Base</Super>
<TimeChanged>64135,42939.617099</TimeChanged>
<TimeCreated>64090,36037.285103</TimeCreated>

<Parameter name="evenRowClass">
<Type>%String</Type>
<Default>evenTableRow</Default>
</Parameter>

<Parameter name="oddRowClass">
<Type>%String</Type>
<Default>oddTableRow</Default>
</Parameter>

<Method name="run">
<FormalSpec>parameters:EasyCSP.Core.HashArray=""</FormalSpec>
<Implementation><![CDATA[
    
    if '..data.isset("model") {
        throw ##class(EasyCSP.Core.Exception).%New($classname(%EasyCSP.controller.layout.view), 404,"""Model"" should be a valid instance of an EasyCSP.Model.Base extent.")
    }
    
    set ..pageTitle = ..data.get("pageTitle")
    set:..pageTitle="" ..pageTitle = "View a Record"
    
    set model = ..data.get("model")
    
    set columnOrder = ..data.get("columnOrder")
    set:columnOrder="" columnOrder = model.columnOrder
    
    set pk = model.PK
    set keyValue = model.getAttributeValue(pk)
    
    set attributes = model.getAttributes()
    
    set htmlOptions = ##class(EasyCSP.Core.HashArray).%New()
    do htmlOptions.set("class", "activeTable")
    do ..openTag("table", htmlOptions)
    
    set keyAttribute = attributes.get(pk)
    do ..addContent(..tag("caption", "", "Record ID: "_keyAttribute.value, 1))
    
    // table head
    do ..openTag("thead")
    do ..openTag("tr")
    do ..addContent(..tag("th", "", "Field", 1))
    do ..addContent(..tag("th", "", "Value", 1))
    do ..closeTag("tr")
    do ..closeTag("thead")
    
    do ..openTag("tbody")
    
    set trOptions = ##class(EasyCSP.Core.HashArray).%New()
    
    set displayCounter = columnOrder.Next("")
    while displayCounter '= "" {
        set finder = columnOrder.get(displayCounter)
        set attribute = attributes.get(finder)
        if attribute.isPK=1 {
            set displayCounter = columnOrder.Next(displayCounter)
            continue
        }
        
        if (displayCounter#2) = 1 {
            set rowClass = ..#oddRowClass
        } else {
            set rowClass = ..#evenRowClass
        }
        
        do trOptions.set("class", rowClass)
    
        do ..openTag("tr", trOptions)
        do ..addContent(..tag("td", "", attribute.label, 1))
        do ..addContent(..tag("td", "", attribute.toDisplay(), 1))
        do ..closeTag("tr")
        
        set displayCounter = columnOrder.Next(displayCounter)
    }
    
    do ..closeTag("tbody")
    
    do ..closeTag("table")
    
    do htmlOptions.unsetAll()
    do htmlOptions.set("style","float: right; width: 30%;")
    do ..openTag("div", htmlOptions)
    set controllerName = $mvoconv(%EasyCSPApp.controller.id, "MCT")
    do ..addContent(..routeLink("Cancel and return to the " _ controllerName _ " index", %EasyCSPApp.controller.id _ "/index"))
    do ..closeTag("div")
    
    quit 1
]]></Implementation>
</Method>
</Class>
</Export>
